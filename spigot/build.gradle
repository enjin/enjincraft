import org.apache.tools.ant.filters.ReplaceTokens

final String LIBS = "com.enjin.enjincraft.spigot.libs"

jar.archiveBaseName = 'EnjinCraft-Spigot'

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    // Server provided dependencies
    shadow deps.spigot
    shadow deps.sqlite
    // Plugin provided dependencies
    shadow deps.placeholderApi
    // EnjinCraft provided dependencies
    implementation deps.minecraftCommons
    implementation deps.nbtapi
    implementation deps.kyoriText
    implementation deps.kyoriTextAdapterBukkit

    api (deps.enjinSdk) {
        exclude module: "gson"
    }
}

// Exclude GSON as this is fetched at runtime.
shadowJar {
    relocate("com.fasterxml.jackson", "${LIBS}.jackson")
    relocate("com.pusher", "${LIBS}.pusher")
    relocate("de.tr7zw.annotations", "${LIBS}.tr7zw.annotations")
    relocate("de.tr7zw.changeme", "${LIBS}.tr7zw")
    relocate("io.sentry", "${LIBS}.sentry")
    relocate("kotlin", "${LIBS}.kotlin")
    relocate("net.kyori", "${LIBS}.kyori")
    relocate("okhttp3", "${LIBS}.okhttp3")
    relocate("okio", "${LIBS}.okio")
    relocate("org.checkerframework", "${LIBS}.checkerframework")
    relocate("org.intellij", "${LIBS}.intellij")
    relocate("org.java_websocket", "${LIBS}.java_websocket")
    relocate("org.jetbrains", "${LIBS}.jetbrains")
    relocate("org.slf4j", "${LIBS}.slf4j")
    relocate("retrofit2", "${LIBS}.retrofit2")
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property("version")
    ]
}
